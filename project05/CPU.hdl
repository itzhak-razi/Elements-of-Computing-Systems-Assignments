// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Works
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=AIn); 
    Not(in=instruction[15], out=notFifteen);
    Or(a=instruction[5], b=notFifteen, out=loadA);
    ARegister(in=AIn, load=loadA, out=AOut, out[0..14]=addressM); 
     
    Mux16(a=AOut, b=inM, sel=instruction[12], out=AOrM); 
   
    And(a=instruction[4], b=instruction[15], out=loadD); 
    DRegister(in=ALUOut, load=loadD, out=DOut); 

    ALU(x=DOut, y=AOrM, zx=instruction[11], nx=instruction[10], zy=instruction[9], 
        ny=instruction[8],f=instruction[7], no=instruction[6], zr=isZero, ng=isNegative, 
        out=ALUOut, out=outM);

   Not(in=isZero, out=notZero);
   Not(in=isNegative, out=notNegative);
   And(a=notZero, b=notNegative, out=positiveOut);
   And(a=instruction[0], b=positiveOut, out=jumpIfPos);

   And(a=instruction[1], b=isZero, out=jumpIfZero);
    
   And(a=instruction[2], b=isNegative, out=jumpIfNeg);
    
   Or(a=jumpIfPos, b=jumpIfNeg, out=maybeJump);
   Or(a=maybeJump, b=jumpIfZero, out=jumpStatus);

   And(a=jumpStatus, b=instruction[15], out=jump);
   Not(in=jump, out=incStatus);
    
   PC(in=AOut, reset=reset, inc=incStatus, load=jump, out[0..14]=pc);

   Not(in=instruction[3], out=notWriteM);
   Not(in=notWriteM, out=writeMStatus);
   And(a=instruction[15], b=writeMStatus, out=writeM);
}

